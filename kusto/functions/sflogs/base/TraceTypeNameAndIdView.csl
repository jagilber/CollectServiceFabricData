.create-or-alter function with (docstring = "[T:string] [typeMatch:string] function to split Type column into TypeName and TypeId", folder = "sflogs/base")
    TraceTypeNameAndIdView(T:string, typeMatch:string="") {
        table(T)
        | where iif(isempty(typeMatch), true, Type matches regex strcat('((?i)', typeMatch,')'))
        | extend split = indexof(Type,'@')
        | extend TypeId = iif(split > 0, substring(Type, split + 1) ,'')
        | extend TypeName = iif(split > 0, substring(Type, 0, split) ,Type)
        | project-away split
        | order by Timestamp asc
    }
